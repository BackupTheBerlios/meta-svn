|
	& merge (merge gtk-sharp pango-sharp) mscorlib
	. Application.Init 0
	. console.create 0
	. console.write "hi"
	win Window.[/.mscorlib.String] "TextViewSample"
	win.DeleteEvent
		|Application.Quit 0
	. win.SetDefaultSize
		. 600
		. 400
	view TextView.[0] 0
	view.KeyPressEvent
		e|if (equal e.[2].Event.Key gdk-sharp.Key.Return)
			then
				|print "equal!!!"
			else
				|print "not equal!!"
	menuBar MenuBar.[0] 0
	fileItem MenuItem.[String] "File"
	fileMenu Menu.[0] 0
	fileItem.Submenu fileMenu
	menu
		file
			exit MenuItem.[String] "Exit"
			exit.Activated
				|win.Destroy 0
			open MenuItem.[String] "Open..."
			open.Activated
				|
					dialog FileChooserDialog.[
						. String
						. Window
						. FileChooserAction
						. Array.[Object]
					]
						. "titulo de prueba"
						. win
						. FileChooserAction.Open
						. 0
					. dialog.AddButton.[
						. String
						. ResponseType
					] 
						. "Cancel"
						. ResponseType.Cancel
					. dialog.AddButton.[
						. String
						. ResponseType
					]
						. "Accept"
						. ResponseType.Accept
					dialog.Filter FileFilter.[0] 0
					. dialog.Filter.AddPattern "*.meta"
					result dialog.Run 0
					. if (equal result ResponseType.Accept)
						then
							|
								$file dialog.Filename
								$view.Buffer.Text File.ReadAllText file
					. dialog.Destroy 0
			save MenuItem.[String] "Save"
			save.Activated
				|if (not (equal file 0))
					then
						|File.WriteAllText
							. file
							. view.Buffer.Text
		edit
			copy 0
			cut 0
			paste 0
	. fileMenu.Append menu.file.open
	. fileMenu.Append menu.file.save
	. fileMenu.Append menu.file.exit
	. menuBar.Append fileItem
	box VBox.[
		. Boolean
		. Int32
	]
		. 0
		. 2
	scrolled ScrolledWindow.[0] 0
	. scrolled.Add view
	. box.PackStart.[
		. Widget
		. Boolean
		. Boolean
		. UInt32
	]
		. menuBar
		. 0
		. 0
		. 0
	. box.PackStart.[
		. Widget
		. Boolean
		. Boolean
		. UInt32
	]
		. scrolled
		. 1
		. 1
		. 0
	. win.Add box
	font FontDescription.[0] 0
	font.Family "Courier New"
	font.Size 10
	. view.ModifyFont font
	tabs TabArray.[
		. Int32
		. Boolean
	]
		. 3
		. 0
	. tabs.SetTab
		. 0
		. TabAlign.Left
		. 0
	. tabs.SetTab
		. 1
		. TabAlign.Left
		. 2
	view.Tabs tabs
	win.Title "Meta IDE"
	. win.Maximize 0
	. win.Add scrolled
	. win.ShowAll 0
	. Application.Run 0