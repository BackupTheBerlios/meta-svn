parser|
	defaultFunction
		default|
			arg|default.function merge
				. default.argument
				. arg			
	rule defaultFunction
		function
			arg|
				parser|
					map arg.condition parser
					current if
						condition succeeded map
						then
							|merge
								. parser
								. merge
									. map
									. arg.result merge
										. parser
										. map
									. 
										parsed arg.parsing map.parsed
		argument
			result
				|0
			parsing
				p|p
	charRule
		compare|rule
			condition
				parser|if
					condition and
						.
							|not null parser.text
						. 
							|compare parser.text.[1]
					then
						|
							parsed parser.text.[1]
							text rest parser.text
	character
		char|charRule
			input|equal
				. input
				. char
	characterSet
		chars|charRule
			input|contains
				array chars
				value input
	characterExcept
		chars|charRule
			input|not contains
				value input
				array chars
	stringRule
		string|sequence apply
			array string
			function
				char|character char
	zeroOrMore
		loopRule|rule
			condition
				parser|
					implementation
						p|
							matched loopRule p
							current if	
								condition succeeded matched
								then
									|
										nextMatched implementation matched
										current if
											condition succeeded nextMatched
											then
												|
													parsed join
														. 
															. matched.parsed
														. nextMatched.parsed
													text nextMatched.text
								else
									|p
					current implementation
						parsed 0
						text parser.text
	oneOrMore
		arg|rule
			condition
				parser|
					loop zeroOrMore arg
					loopResult loop
						text parser.text
					current if
						condition and
							.
								|succeeded loopResult
							.
								|not null loopResult.parsed
						then
							|loopResult
	sequence
		rules|rule
			condition
				parser|
					implementation
						arg|if
							condition hasKey
								map arg.rules
								key 1
							then
								|
									ruleResult arg.rules.[1] arg.parser
									current if
										condition succeeded ruleResult
										then
											|
												nextResult implementation
													parser
														parsed 0
														text ruleResult.text
														indentationCount 0
													rules rest arg.rules
												current if
													condition succeeded nextResult
													then
														|
															text nextResult.text
															parsed join
																.
																	. ruleResult.parsed
																. nextResult.parsed
													else
														|nextResult
							else
								|arg.parser
					current implementation
						parser parser
						rules rules
	prePost
		arg|rule
			condition sequence join
				. 
					. arg.pre
					. arg.rule
				. if
					condition hasKey
						map arg
						key "post"
					then
						|
							. arg.post
			parsing
				arg|arg.[2]
	succeeded
		arg|hasKey
			map arg
			key "parsed"
	alternatives
		alts|rule
			condition
				parser|
					implementation
						arg|if
							condition not null arg
							then
								|
									altResult arg.[1] parser
									current if
										condition succeeded altResult
										then
											|
												parsed altResult.parsed
												text altResult.text
										else
											|implementation rest arg
					current implementation alts
	loopRule
		arg|rule
			condition
				parser|
					implementation
						impArg|if
							condition arg.tooMuch
								count add
									. impArg.n
									. 1
								parser impArg.parser
							then
								|impArg.parser
							else
								|
									res arg.rule impArg.parser
									current if
										condition succeeded res
										then
											|
												nextResult implementation
													parser res
													n add
														. impArg.n
														. 1
												current if
													condition succeeded nextResult
													then
														|
															parsed join
																. 
																	. res.parsed
																. nextResult.parsed
															text nextResult.text
															indentationCount nextResult.indentationCount
										else
											|if
												condition not arg.tooFew
													count subtract
														. impArg.n
														. 1
													parser impArg.parser
												then
													|impArg.parser
					current implementation
						parser parser
						n 0
	nTimes
		arg|loopRule
			tooMuch
				upperArg|strictlyIncreasing
					. arg.n upperArg.parser
					. upperArg.count
			tooFew
				lowerArg|strictlyIncreasing
					. lowerArg.count
					. arg.n lowerArg.parser
			rule arg.rule
	optional
		subRule|
			parser|merge
				.
					parsed 0
					text parser.text
				. subRule parser
	lookupForbidden characterExcept
		. ' '
		. 9
		. 13
		. 10
		. '.'
		. '['
		. ']'
	number rule
		condition oneOrMore characterSet "0123456789"
		parsing
			arg|/.Meta.Number.Parse arg
	newLine alternatives
		. character 10
		. sequence
			. character 13
			. character 10
	whitespace zeroOrMore characterSet
		. 9
		. ' '
	endOfLine sequence
		. whitespace
		. newLine
	indentRule
		change|rule
			condition nTimes
				n
					parser|add
						. parser.indentationCount
						. change
				rule character 9
			result
				parser|
					indentationCount add
						. parser.indentationCount
						. change
	indentation indentRule 1
	sameIndentation indentRule 0
	dedentation indentRule -1
	stringLine zeroOrMore characterExcept
		. 10
		. 13
		. '"'
	characterDataExpression prePost
		pre character 39
		rule characterExcept
			. 39
		post character 39
	string prePost
		pre character '"'
		rule alternatives
			. oneOrMore characterExcept
				. 10
				. 13
				. '"'
			. rule
				condition sequence
					. rule
						condition sequence
							. newLine
							. indentation
							. stringLine
						parsing
							arg|arg.[3]
		post sequence
			. optional newLine
			. optional character '"'
	lookupString oneOrMore lookupForbidden
	value
		parser|
			rule alternatives
				. map
				. string
				. number
				. characterDataExpression
			current rule parser.text
	lookupAnything prePost
		pre character '['
		rule value
		post sequence
			. zeroOrMore 9
			. character ']'
	entry rule
		condition sequence
			. function
			. alternatives
				. lookupString
				. lookupAnything
			. whitespace
			. value
		parsing
			arg|
				[arg.[2]] arg.[4]
	onePlusX
		arg|rule
			condition sequence
				. rule
					condition arg.first
					parsing
						arg|
							. arg
				. zeroOrMore rule
					condition prePost
						pre arg.delimiter
						rule arg.step
			parsing
				arg|join arg
	mapCore onePlusX
		first entry
		delimiter sameIndentation
		step entry
	map prePost
		pre indentation
		rule mapCore
		post dedentation
	file mapCore
	expression
		parser|
			localRule rule
				condition alternatives
					. literalExpression
			current localRule
				text parser.text
	function rule
		condition sequence
			. zeroOrMore characterExcept
				. '"'
				. ' '
				. 10
			. sequence character ' '
			. expression
		parsing
			arg|
				parameter arg.[1]
				expression arg.[3]
	lookupAnythingExpression prePost
		pre character '['
		rule expression
		post sequence
			. zeroOrMore character 9
			. character ']'
	lookupStringExpression rule
		condition lookupString
		parsing
			arg|
				literal arg
	search rule
		condition alternatives
			. lookupStringExpression
			. lookupAnythingExpression
		parsing
			arg|
				search arg
	root rule
		condition character '/'
		parsing
			|
				root 0
	ruleCurrent rule
		condition stringRule "current"
		parsing
			|
				["current"] 0
	realLookup rule
		condition alternatives
			. lookupStringExpression
			. lookupAnythingExpression
		parsing
			arg|
				lookup arg
	lookup alternatives
		. ruleCurrent
		. realLookup
	select rule
		condition onePlusX
			first alternatives
				. root
				. search
				. lookup
			delimiter character '.'
			step lookup
		parsing
			arg|
				select arg
	functionExpression rule
		condition function
		parsing
			arg|
				key
					.
						lookup
							literal "function"
				value
					literal arg
	statement alternatives
		. functionExpression
		. rule
			condition sequence
				. keys
				. whitespace
				. expression
				. endOfLine
			parsing
				arg|
					key arg.[1]
					value arg.[3]
	program rule
		condition sequence
			.
				parser|
					. "p.defaultKeys.Push 1"
					parsed 0
					text parser.text.text
			. sequence
				. indentation
				. rule
					condition statement
					parsing
						arg|
							. arg
				. zeroOrMore sequence
					. alternatives
						. sameIndentation
						. dedentation
					. statement
			. 
				parser|"p.defaultKeys.Pop 0"
		parsing
			arg|
				program arg.[2]
	call rule
		condition sequence
			. select
			. alternatives
				. prePost
					pre sequence
						. alternatives
							. character ' '
							. character 9
					rule expression
				. program
		parsing
			arg|
				call
					callable arg.[1]
					argument arg.[2]
	emptyMap rule
		condition character '0'
		parsing
			|0
	literalExpression rule
		condition alternatives
			. emptyMap
			. string
			. number
			. characterDataExpression
		parsing
			arg|
				literal arg
	keysSearch prePost
		pre character '$'
		rule search
	autokeyLookup rule
		condition character '.'
		result
			arg|
				defaultKey add
					. arg.defaultKey
					. 1
				parsed
					lookup
						literal arg.defaultKey
	keys onePlusX
		first alternatives
			. keysSearch
			. lookup
			. autokeyLookup
		delimiter character '.'
		step lookup
	current /.Meta.Serialize.ValueFunction
		stringTest
			singleLine string
				text "
					"12344321 asdfasdf"
				"
		numberTest
			integer number
				text "483821"
		lookupStringTest
			simple lookupStringExpression
				text "hello"
			shouldPartlyMatch lookupStringExpression
				text "hello.hello"
		expressionTest
			stringTest expression
				text "
					"hello"
				"
		literalTest
			stringTest literalExpression 
				text "
					"hello"
				"
			characterTest literalExpression 
				text "'a'"
			emptyMapTest literalExpression 
				text "0"
		lookupAnythingTest
			simple lookupAnythingExpression
				text "
					["hello"]
				"
		searchTest search
			text "hello"
		rootTest root
			text "/"
		ruleCurrentTest 
			shouldMatch ruleCurrent
				text "current"
			shouldNotMatch ruleCurrent
				text "hello"
		lookupTest lookup
			text "hello"
		selectTest
			singleTest select
				text "hello"
			severalTest select
				text "hello.hello"
			complexTest select
				text"
					hello.["world"]
				"
		keysSearchTest
			simple keysSearch
				text "$hello"
		keysTest
			simple keys
				text "hello.hello"
		callTest
			simple call
				text "
					hello "hi"
				"
		endOfLineTest
			unix
				shouldMatch endOfLine
					text
						. 10
				shouldNotMatch endOfLine
					text "hello"
			windows 
				shouldMatch endOfLine
					text 
						. 13
						. 10
				shouldNotMatch endOfLine
					text "hello"
		whitespaceTest
			shouldMatch whitespace
				text "					 "
			shouldNotMatch whitespace
				text "			kj	"
		indentationTest
			shouldMatch indentation
				text "	"
				indentationCount 0
		dedentationTest
			shouldMatch
				two dedentation
					text "			"
					indentationCount 2
					parsed 0
				one dedentation
					text "		"
					indentationCount 1
					parsed 0
			shouldNotMatch dedentation
				text "		"
				indentationCount 5
				parsed 0
		sameIndentationTest
			shouldMatch
				two sameIndentation
					text "			"
					indentationCount 2
				one sameIndentation
					text "		"
					indentationCount 1
			shouldNotMatch sameIndentation
				text "		"
				indentationCount 5
		stringLineTest stringLine
			text "
				hello hello hello
				hi hi
			"
		longStringTest 
			simple string
				text "
					"
						hello hello hello
					"
				"
				indentationCount 0
				parsed 0
			longer string
				text "
					"
						hello
						hello
						hello
					"
				"
				indentationCount 0
				parsed 0
