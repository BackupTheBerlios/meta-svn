|,
	&=-
		mergeAll
		+
			System
			PresentationFramework
			PresentationCore
			windowsbase
			mscorlib
			Meta
	listProgramView=,
		create=,
			|-
				listProgramView
				,
					program=+
						,
							key=+
								,
									lookup=,
										literal=1
							value=,
								literal=" "
		source|,
			&=-
				control
				,
					Children=-
						apply
						.
							source
							program
						,
							statement|-
								parseView
								.
									statement
									value
					Background=-
						.
							Brushes
							get_Green
					KeyDown=-
						keyMapping
						keys
			getMap=,
				|,
					program=-
						foreach
						-
							enumerableToArray
							Children
						,
							key|,
								value|,
									key=+
										,
											lookup=,
												literal=key
									value=-
										.
											value
											getMap
			keys=,
				.
					ModifierKeys
					Alt=,
					.
						Key
						Enter=,
						|-
							.
								Children
								Add_UIElement
							-
								view
								-
									.
										literalView
										create
	emptyView=,
		|,
			&=-
				textBox
			Background=-
				.
					Brushes
					get_Red
			PreviewTextInput=,
				|,
					e|,
						.
							!e
							Handled=1
			getMap=,
				|0
	textBox=,
		|,
			&=-
				TextBox
			AcceptsReturn=1
			GotFocus=,
				|,
					|-
						SelectAll
	horizontal=.
		Orientation
		Horizontal
	stringView=,
		create=,
			|-
				stringView
				" "
		text|,
			&=-
				textBox
				,
					Text=text
					Background=-
						.
							Brushes
							get_Fuchsia
					getMap=,
						|-
							get_Text
	numberView=,
		number|,
			&=-
				textBox
				,
					Background=-
						.
							Brushes
							get_LightYellow
					Text=-
						integerToString
						number
					getMap=,
						|-
							stringToInteger
							-
								get_Text
					PreviewTextInput=,
						|,
							e|-
								apply
								-
									.
										e
										get_Text
								,
									c|-
										if
										-
											not
											-
												.
													Char
													IsDigit_Char
												c
										,
											|-
												.
													!e
													set_Handled_Boolean
												1
	multi=,
		init|,
			&=-
				StackPanel
				-
					merge
					,
						Margin=-
							Thickness_Double
							2
						Orientation=.
							PresentationFramework
							Orientation
							Vertical
						.
							FrameworkElement
							HorizontalAlignmentProperty
						=.
							PresentationFramework
							HorizontalAlignment
							Left
						MouseLeftButtonDown=,
							|,
								e|,
									.
										!e
										Handled=1
									asdf=-
										.
											panel
											Focus
					init
	entry=,
		create=,
			|-
				entry
				-
					.
						stringView
						create
				-
					.
						stringView
						create
		key|,
			value|,
				&=-
					control
					,
						Children=+
							key
							value
						Orientation=.
							PresentationFramework
							Orientation
							Horizontal
						KeyDown=-
							keyMapping
							,
								.
									ModifierKeys
									Alt=,
									.
										Key
										Enter=,
										|-
											.
												Parent
												Children
												Insert_Int32_UIElement
											-
												add
												-
													.
														Parent
														Children
														IndexOf_UIElement
													this
												1
											-
												.
													entry
													create
				getKey=,
					|-
						.
							Children
							0
							getMap
				getValue=,
					|-
						.
							Children
							1
							getMap
	copied=0
	view=,
		content|,
			&=-
				multi
				,
					Children=+
						content
					Margin=-
						Thickness_Double
						2
					Orientation=horizontal
					.
						FrameworkElement
						HorizontalAlignmentProperty
					=.
						PresentationFramework
						HorizontalAlignment
						Left
					.
						FrameworkElement
						VerticalAlignmentProperty
					=.
						PresentationFramework
						VerticalAlignment
						Top
			getMap=,
				|-
					.
						Children
						0
						getMap
			setView=,
				view|,
					!Children=+
						view
			keys=,
				.
					ModifierKeys
					Alt
				=-
					merge
					,
						.
							Key
							B
						=,
							|,
								!copied=-
									getMap
						.
							Key
							M
						=,
							|-
								setView
								-
									parseView
									c
						.
							Key
							R
						=,
							|-
								.
									commands
									Debug
									Run
						.
							Key
							Delete
						=,
							|-
								.
									Parent
									Children
									Remove_UIElement
								this
					-
						mergeAll
						-
							foreach
							,
								.
									Key
									U
								=stringView
								.
									Key
									V
								=.
									lookupView
									create
								.
									Key
									L
								=.
									literalView
									create
								.
									Key
									N
								=numberView
								.
									Key
									E
								=emptyView
								.
									Key
									A
								=.
									listProgramView
									create
								.
									Key
									P
								=.
									programView
									create
								.
									Key
									S
								=.
									selectView
									create
								.
									Key
									T
								=.
									currentView
									create
								.
									Key
									F
								=.
									searchView
									create
								.
									Key
									C
								=.
									callView
									create
							,
								key|,
									value|,
										key=,
											|+
												-
													setView
													-
														value
														0
												-
													.
														Children
														0
														Focus
			KeyDown=-
				keyMapping
				keys
	keyMapping=,
		keys|,
			sender|,
				e|-
					callIf
					keys
					.
						e
						KeyboardDevice
						Modifiers
					,
						functions|+
							-
								print
								"in keyMapping"
							-
								callIf
								functions
								.
									e
									SystemKey
								,
									func|,
										asdf=-
											print
											.
												e
												SystemKey
										asdf=-
											func
										.
											!e
											Handled=1
	control=,
		init|,
			&=-
				multi
				-
					merge
					,
						Focusable=1
					init
	functionView=,
		key|,
			value|,
				&=-
					control
					,
						Children=+
							-
								view
								-
									stringView
									.
										value
										parameter
							-
								parseView
								.
									value
									expression
						Background=-
							.
								Brushes
								get_Azure
				getKey=,
					|"function"
				getValue=,
					|,
						parameter=-
							.
								Children
								0
								getMap
						expression=-
							.
								Children
								1
								getMap
	statementBase=,
		children|,
			keys=,
				.
					ModifierKeys
					Alt
				=,
					.
						Key
						Y
					=,
						|-
							.
								Parent
								Parent
								Children
								Insert_Int32_UIElement
							-
								add
								-
									.
										Parent
										Parent
										Children
										IndexOf_UIElement
									Parent
								1
							-
								view
								-
									.
										functionStatementView
										create
					.
						Key
						Enter
					=,
						|-
							.
								Parent
								Parent
								Children
								Insert_Int32_UIElement
							-
								add
								-
									.
										Parent
										Parent
										Children
										IndexOf_UIElement
									Parent
								1
							-
								view
								-
									.
										statementView
										create
			&=-
				control
				,
					Children=children
					KeyDown=-
						keyMapping
						keys
	statementView=,
		create=,
			|-
				statementView
				,
					key=+
						,
							lookup=,
								literal=" "
					value=,
						literal=" "
		source|,
			&=-
				statementBase
				+
					-
						parseView
						,
							select=.
								source
								key
					-
						parseView
						.
							source
							value
			Orientation=.
				PresentationFramework
				Orientation
				Horizontal
			getMap=,
				|,
					key=.
						-
							.
								Children
								0
								getMap
						select
					value=-
						.
							Children
							1
							getMap
			Background=-
				.
					Brushes
					get_Gray
	functionStatementView=,
		create=,
			|-
				functionStatementView
				,
					value=,
						literal=,
							parameter="arg"
							expression=,
								literal="hello"
		source|,
			&=-
				statementBase
				+
					-
						view
						-
							stringView
							.
								source
								value
								literal
								parameter
					-
						parseView
						.
							source
							value
							literal
							expression
			getMap=,
				|,
					key=+
						,
							lookup=,
								literal="function"
					value=,
						literal=,
							parameter=-
								.
									Children
									0
									getMap
							expression=-
								.
									Children
									1
									getMap
			Background=-
				.
					Brushes
					get_Azure
	programView=,
		create=,
			|-
				programView
				,
					program=+
						,
							key=+
								,
									lookup=,
										literal=" "
							value=,
								literal=" "
		source|,
			&=-
				control
				,
					Background=-
						.
							Brushes
							get_Blue
					Children=-
						apply
						.
							source
							program
						,
							statement|-
								view
								-
									ifElse
									-
										equal
										.
											statement
											key
										+
											,
												lookup=,
													literal="function"
									,
										|-
											functionStatementView
											statement
									,
										|-
											statementView
											statement
			getMap=,
				|,
					program=-
						apply
						-
							enumerableToArray
							Children
						,
							entry|-
								.
									entry
									getMap
	mapView=,
		create=,
			|-
				mapView
				,
					"hello"="world"
		entries|,
			&=-
				control
				,
					Children=-
						foreach
						entries
						,
							key|,
								value|-
									ifElse
									-
										equal
										key
										"function"
									,
										|-
											functionView
											key
											value
									,
										|-
											entry
											-
												parseView
												key
											-
												parseView
												value
			getMap=,
				|-
					sum
					-
						apply
						-
							enumerableToArray
							Children
						,
							entry|,
								<-
									.
										entry
										getKey=-
									.
										entry
										getValue
					merge
	callView=,
		create=,
			|-
				callView
				,
					call=+
						,
							select=+
								,
									search=,
										literal="hello"
						,
							literal="world"
		source|,
			&=-
				control
				,
					Children=-
						apply
						.
							source
							call
						,
							arg|-
								parseView
								arg
					Background=-
						.
							Brushes
							get_BurlyWood
					KeyDown=-
						keyMapping
						keys
			getMap=,
				|,
					call=-
						apply
						-
							enumerableToArray
							Children
						,
							child|-
								.
									child
									getMap
			keys=,
				.
					ModifierKeys
					Alt
				=,
					.
						Key
						Enter
					=,
						|-
							.
								Children
								Add_UIElement
							-
								view
								-
									.
										literalView
										create
	lookupView=,
		create=,
			|-
				lookupView
				,
					lookup=,
						literal="hello"
		source|,
			&=-
				control
				,
					Children=+
						-
							parseView
							.
								source
								lookup
					Background=-
						.
							Brushes
							get_MistyRose
			getMap=,
				|,
					lookup=-
						.
							Children
							0
							getMap
	searchView=,
		create=,
			|-
				searchView
				,
					search=,
						literal="hello"
		source|,
			&=-
				control
				,
					Children=+
						-
							parseView
							.
								source
								search
					Background=-
						.
							Brushes
							get_DeepSkyBlue
			getMap=,
				|,
					search=-
						.
							Children
							0
							getMap
	literalView=,
		create=,
			|-
				literalView
				,
					literal=" "
		source|,
			&=-
				control
				,
					Children=+
						-
							parseView
							.
								source
								literal
					Background=-
						.
							Brushes
							get_DarkViolet
			getMap=,
				|,
					literal=-
						.
							Children
							0
							getMap
	currentView=,
		create=,
			|-
				currentView
		|,
			&=-
				textBox
				,
					Background=-
						.
							Brushes
							get_White
					PreviewTextInput=,
						|,
							e|,
								.
									!e
									Handled=1
					getMap=,
						|,
							current=0
	selectView=,
		create=,
			|-
				selectView
				,
					select=+
						,
							search=,
								literal="hello"
		source|,
			keys=,
				.
					ModifierKeys
					Alt
				=,
					.
						Key
						Enter
					=,
						|-
							.
								Children
								Add_UIElement
							-
								view
								-
									.
										lookupView
										create
			&=-
				control
				,
					Children=-
						apply
						.
							source
							select
						,
							arg|-
								parseView
								arg
					KeyDown=-
						keyMapping
						keys
					Background=-
						.
							Brushes
							get_Chocolate
			getMap=,
				|,
					select=-
						apply
						-
							enumerableToArray
							Children
						,
							child|-
								.
									child
									getMap
	parseView=,
		arg|-
			view
			-
				switch
				arg
				,
					!"empty"=emptyView
					!"isString"=stringView
					!"isNumber"=numberView
					,
						|-
							contains
							arg
							"call"
					=callView
					,
						|-
							contains
							arg
							"literal"
					=literalView
					,
						|-
							contains
							arg
							"search"
					=searchView
					,
						|-
							contains
							arg
							"lookup"
					=lookupView
					,
						|-
							contains
							arg
							"select"
					=selectView
					,
						|-
							contains
							arg
							"current"
					=currentView
					,
						|-
							if
							-
								contains
								arg
								"program"
							,
								|-
									sum
									-
										foreach
										-
											range
											-
												count
												.
													arg
													program
										,
											key|,
												value|-
													equal
													.
														arg
														program
														<key
														key
													+
														,
															lookup=,
																literal=key
									and
					=listProgramView
					,
						|-
							contains
							arg
							"program"
					=programView
					,
						|1
					=,
						|-
							mapView
							arg
	load=,
		|-
			.
				scrollViewer
				set_Content_Object
			-
				parseView
				-
					parse
					path
	currentData=,
		|-
			.
				panel
				Children
				1
				Content
				getMap
	commands=+
		,
			header="File"
			items=+
				,
					header="Open"
					click=,
						|-
							.
								-
									OpenFileDialog
									,
										Filter="*.meta (Meta files)|*.meta"
										FileOk=,
											dialog|,
												|,
													!path=.
														dialog
														FileName
													-
														load
								ShowDialog
				,
					header="SaveAs"
					click=,
						|-
							.
								-
									SaveFileDialog
									,
										Filter="*.meta (Meta files)|*.meta"
										DefaultExt=".meta"
										AddExtension=1
										FileOk=,
											dialog|,
												|,
													!path=.
														dialog
														FileName
													-
														Save
								ShowDialog
				,
					header="Save"
					click=,
						|-
							ifElse
							-
								empty
								path
							,
								|-
									SaveAs
							,
								|-
									.
										Binary
										Serialize_Map_String
									-
										currentData
									path
				,
					header="Exit"
					click=,
						|-
							.
								window
								Close
		,
			header="Debug"
			items=+
				,
					header="Run"
					click=,
						|+
							-
								print
								-
									currentData
							-
								.
									File
									Save
							-
								.
									Process
									Start_String_String
								"C:\Meta\0.2\Meta.exe"
								path
	menu=-
		Menu
		,
			.
				DockPanel
				DockProperty
			=.
				Dock
				Top
			Items=-
				apply
				commands
				,
					value|-
						MenuItem
						,
							Header=.
								value
								header
							Items=-
								apply
								.
									value
									items
								,
									value|-
										.
											MenuItem
										,
											Header=.
												value
												header
											Click=,
												sender|.
													value
													click
	scrollViewer=-
		ScrollViewer
		,
			Content=-
				parseView
				,
					literal=" "
	path="C:\asdf.meta"
	-
		load
		path
	panel=-
		DockPanel
		,
			Children=+
				menu
				scrollViewer
	window=-
		Window
		,
			Content=panel
			Title="Meta Edit"
	1=-
		.
			-
				Application
			Run_Window
		window