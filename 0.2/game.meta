|,
	&=-
		mergeAll
		+
			System.Drawing
			SdlDotNet
			-
				.
					SdlDotNet
					Video
					SetVideoModeWindow_Int32_Int32_Boolean
				800
				600
				1
			.
				SdlDotNet
				Events
			.
				SdlDotNet
				Key
			OdeDotNet
	world=-
		World
	space=-
		Space
	upper=-
		.
			space
			CreateBox_Single_Single_Single
		800
		20
		20
	-
		.
			upper
			set_Position_Vector3
		-
			Vector3_Single_Single_Single
			400
			0
			0
	lower=-
		.
			space
			CreateBox_Single_Single_Single
		800
		20
		20
	-
		.
			lower
			set_Position_Vector3
		-
			Vector3_Single_Single_Single
			400
			600
			0
	left=-
		.
			space
			CreateBox_Single_Single_Single
		20
		600
		20
	-
		.
			left
			set_Position_Vector3
		-
			Vector3_Single_Single_Single
			0
			300
			0
	right=-
		.
			space
			CreateBox_Single_Single_Single
		20
		600
		20
	-
		.
			right
			set_Position_Vector3
		-
			Vector3_Single_Single_Single
			800
			300
			0
	bubble=initSize?,
		body=-
			Body_World
			world
		&=-
			merge
			body
			,
				body=body
		size=-
			ElseIf
			-
				empty
				initSize
			?-
				random
				5
			?initSize
		radius=?-
			Product
			size
			4
		-
			set_Mass_Mass
			-
				.
					Mass
					Sphere_Single_Single
				-
					Product
					2
					-
						Product
						size
						size
				1/1000
		keyUp=0
		acceleration=+
			0
			0
		setSpeed=s?,
			speed:s
		keyDown=0
		position=+
			-
				random
				790
			-
				random
				590
		-
			set_Position_Vector3
			-
				Vector3_Single_Single_Single
				.
					position
					1
				.
					position
					2
				0
		geom=-
			.
				space
				CreateSphere_Single
			-
				radius
		-
			.
				geom
				set_Position_Vector3
			-
				get_Position
		-
			.
				geom
				set_Body_Body
			body
		speed=+
			-
				Difference
				-
					random
					12
				6
			-
				Difference
				-
					random
					12
				6
		-
			set_LinearVel_Vector3
			-
				Vector3_Single_Single_Single
				.
					speed
					1
				.
					speed
					2
				0
		color=-
			.
				Color
				get_Red
		setColor=,
			c|,
				color:c
		GetPosition=?+
			-
				.
					-
						get_Position
					get_X
			-
				.
					-
						get_Position
					get_Y
		move=?0
		draw=?-
			DrawFilledCircle_Circle_Color
			+
				-
					GetPosition
				+
					-
						radius
			color
	player=key?c?,
		this=-
			bubble
			1
		&=-
			merge
			this
			,
				this=this
		jointGroup=-
			JointGroup_Int32
			0
		-
			setColor
			c
		currentJoint=0
		setCurrentJoint=joint?,
			currentJoint:joint
		keyDown=,
			<key=?,
				others=-
					discard
					-
						JoinAll
						+
							bubbles
							players
					bubble?-
						equal
						this
						bubble
				lengths=-
					Map
					others
					b?-
						LengthVector
						-
							DifferenceVector
							-
								GetPosition
							-
								.
									b
									GetPosition
				index=.
					-
						Sort
						-
							keys
							others
						a?b?-
							CompareNumber
							.
								lengths
								<a
							.
								lengths
								<b
					1
				joint=-
					HingeJoint_World_JointGroup
					world
					jointGroup
				-
					setCurrentJoint
					joint
				-
					.
						joint
						Attach_Body_Body
					.
						others
						<index
					this
				-
					.
						joint
						set_Anchor_Vector3
					-
						.
							others
							<index
							get_Position
		keyUp=,
			<key=?,
				-
					.
						jointGroup
						Empty
				currentJoint:0
	paused=0
	-
		add_Quit_QuitEventHandler
		?e?,
			-
				.
					Events
					Close
			-
				.
					Events
					QuitApplication
	-
		add_AppActive_ActiveEventHandler
		?e?-
			If
			-
				not
				-
					equal
					1
					-
						.
							e
							get_State
			,
				|,
					paused:-
						not
						-
							.
								e
								get_GainedFocus
	-
		add_KeyboardDown_KeyboardEventHandler
		?e?-
			Map
			players
			,
				player|-
					CallKey
					.
						player
						keyDown
					-
						print
						-
							.
								e
								get_Key
	-
		.
			add_KeyboardUp_KeyboardEventHandler
		?e?-
			Map
			players
			,
				player|-
					CallKey
					.
						player
						keyUp
					-
						.
							e
							get_Key
	contactGroup=-
		JointGroup_Int32
		0
	-
		.
			space
			add_Collision_CollisionEventHandler
		?e?,
			a=-
				.
					e
					get_Geom1
			b=-
				.
					e
					get_Geom2
			-
				Map
				-
					enumerableToArray
					-
						.
							a
							Collide_Geom_Int32
						b
						13
				contactGeom?,
					contact=-
						Contact
						,
							Geom=contactGeom
					-
						.
							-
								.
									world
									CreateContactJoint_Contact_JointGroup
								contact
								contactGroup
							Attach_Body_Body
						-
							.
								a
								get_Body
						-
							.
								b
								get_Body
	players=+
		-
			player
			Return
			-
				.
					Color
					get_Blue
	bubbles=-
		JoinAll
		+
			-
				Map
				-
					range
					10
				?-
					bubble
					-
						random
						5
			-
				Map
				-
					range
					10
				?-
					bubble
					-
						random
						2
	-
		add_Tick_TickEventHandler
		??-
			If
			-
				not
				paused
			,
				|,
					-
						.
							contactGroup
							Empty
					-
						.
							-
								.
									Video
									get_Screen
							Fill_Color
						-
							.
								Color
								get_White
					-
						.
							space
							CheckCollisions
					-
						.
							world
							Step
					-
						Map
						bubbles
						,
							b|,
								-
									.
										b
										move
								-
									.
										b
										draw
					-
						Map
						players
						,
							player|-
								.
									player
									draw
					-
						Map
						players
						,
							player|-
								If
								-
									not
									-
										equal
										0
										.
											player
											currentJoint
								,
									|,
										a=-
											.
												player
												currentJoint
												get_Anchor
										b=-
											.
												player
												get_Position
										-
											DrawLine_Line_Color
											-
												Line_Int16_Int16_Int16_Int16
												-
													.
														a
														get_X
												-
													.
														a
														get_Y
												-
													.
														b
														get_X
												-
													.
														b
														get_Y
											-
												.
													Color
													get_Black
					-
						Flip
	-
		Run