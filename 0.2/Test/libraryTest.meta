arg|,
	test=,
		booleanTest=,
			not=,
				true=*
					!"not"
					0
				false=*
					!"not"
					1
			and=,
				true=*
					and
					+
						1
						1
						1
						1
				false=*
					and
					+
						1
						0
						1
						1
			orTest=,
				true=*
					!"or"
					,
						.=,
							arg|1
						.=,
							arg|1
						.=,
							arg|1
				false=*
					!"or"
					,
						.=,
							arg|0
						.=,
							arg|0
						.=,
							arg|0
				otherTrue=*
					!"or"
					,
						.=,
							arg|0
						.=,
							arg|1
						.=,
							arg|0
						.=,
							arg|1
						.=,
							arg|0
		numberTest=,
			positiveAdd=*
				!"add"
				100
				1000
			negativeAdd=*
				!"add"
				-100
				91
			subtractTest=*
				!"add"
				1/4
				*
					!"multiply"
					1/100
					-1
			subtractTest2=*
				!"subtract"
				2
				103
			divideTest=*
				!"divide"
				5
				103
			multiplyInteger=*
				!"multiply"
				19
				1023
			multiplyFraction=*
				!"multiply"
				3/2
				7/3
			smallerDecreasingTest=*
				!"strictlyDecreasing"
				,
					.=4
					.=1
			nonincreasingTest=*
				!"decreasing"
				,
					.=1
					.=1
					.=0
					.=1
			nondecreasingTest=*
				!"increasing"
				,
					.=1
					.=2
					.=3
					.=4
					.=5
					.=6
			strictlyIncreasingTest=*
				!"strictlyIncreasing"
				,
					.=23
					.=2
			smallerTest=*
				!"smaller"
				0
				1
			greaterTest=*
				!"greater"
				3
				5
			divide=*
				!"multiply"
				1000
				*
					!"reciprocal"
					7
		bitwiseTest=,
			or=*
				!"bitwiseOr"
				,
					.=8
					.=20
		mapTest=,
			switchTest=*
				!"switch"
				,
					cases=,
						x=,
							arg|"right"
						y=,
							arg|"wrong"
					control="x"
			ifTest=*
				!"if"
				1
				,
					then=,
						arg|"if test successful"
			append=*
				join
				,
					.="abc, "
					.="die Katze lief im Schnee."
			apply=,
				&=*
					!"apply"
					,
						.=1
						.=2
						.=3
					,
						argument|*
							!"add"
							!"argument"
							1
			keyTestMap=,
				x="hello"
			contains=*
				!"hasKey"
				!"keyTestMap"
				"x"
			keysTest=*
				!"keys"
				,
					jjqwv="aeäquq"
					oere="i ke"
					.="oiq"
			equal=,
				map=,
					true=*
						!"equal"
						,
							x="hello"
							y="world"
						,
							x="hello"
							y="world"
					false=*
						!"equal"
						,
							x="y"
							y="z"
						,
							x="y"
							y="a"
			while=,
				counter=0
				SetCounter=,
					argument|,
						!"counter"=!"argument"
				test=*
					!"while"
					,
						condition=,
							arg|*
								!"strictlyIncreasing"
								,
									.=!"counter"
									.=10
						function=,
							arg|*
								!"SetCounter"
								,
									<0=!"counter"
				&=!"counter"
			lastTest=*
				!"last"
				"hello"
			firstTest=*
				!"first"
				"hello"
			splitTest=*
				!"split"
				,
					.="hello world hello"
					space=" "
					delimiters=,
						.=$
							!"space"
							1
			stringSplitTest=*
				$
					!"string"
					"split"
				"hello world hello"
				,
					.=' '
					.='l'
			tryTest=*
				!"try"
				,
					function=,
						arg|"hello"
					catch=,
						argument|*
							$
								!"argument"
								"ToString"
							0
			sortTest=*
				!"sort"
				,
					.=,
						x=12
						y=234
					.=,
						x=123
						y=3245
					.=,
						x=12
						y=234
					.=,	
						x=245
						y=98834
				,
					first|,
						second|*
							!"if"
							*
								!"strictlyIncreasing"
								,
									.=$
										!"first"
										"x"
									.=$
										!"second"
										"x"
							,
								then=,
									arg|-1
								else=,
									arg|*
										!"if"
										*
											!"strictlyDecreasing"
											,
												.=$
													!"first"
													"x"
												.=$
													!"second"
													"x"
										,
											then=,
												arg|1
											else=,
												arg|0
			integerToString=*
				!"integerToString"
				5
			stringReplace=*
				$
					!"string"
					"replace"
				"hello hello hi"
				"he"
				"i"
		bitwiseTest=*
			!"bitwiseOr"
			,
				.=20
				.=$
					/
					"System.Windows.Forms"
					"Keys"
					"Tab"
		filterTest=*
			!"filter"
			,
				.=,
					.=1
					.=1
					.=3
					.=5
					.=2
				argument|*
					!"equal"
					!"argument"
					1
		findTest=*
			!"find"
			,
				array="hello hello hi"
				value="he"
		findTest2=*
			!"find"
			,
				array="test test"
				value="test test"
		sliceTest=*
			!"slice"
			,
				array="alle meine entchen"
				start=5
				end=11
		firstTest=*
			!"first"
			"a"
		rangeTest=*
			!"range"
			10
		reverseTest=*
			!"reverse"
			"hello"
		findFirstTest=*
			!"findFirst"
			,
				value="hell"
				array="he hi hello"
		foreachTest=*
			!"foreach"
			,
				x="hi"
				y="hello"
			,
				key|,
					value|,
						<!"value"=!"key"
		sumTest=*
			!"sum"
			,
				.=2
				.=5
				.=6
				.=8
			,
				first|,
					second|*
						!"add"
						!"first"
						!"second"
		valuesTest=*
			!"values"
			,
				x="hi"
				y="hello"
		isStringTest=,
			no=*
				!"isString"
				2
			yes=*
				!"isString"
				"hi"
		intersectTest=*
			!"intersect"
			,
				.=,
					apple="green"
					orange="orange"
					banana="yellow"
				.=,
					apple="red"
					orange="yellow"
					strawberries="red"
				.=,
					apple="last"
					orange="green"
		lowerTest=*
			$
				!"string"
				"lower"
			"HeLLo"
		upperTest=*
			$
				!"string"
				"upper"
			"HeLLo"
		parseTest=*
			!"parse"
			"
				asdf="hello"
				x=,
					y="hello"
					z=, 
						a="x"
					arg|*
						!"join"
						"hello "
						!"join"
						!"arg"
						"!"
			"
		<"invertTest"=*
			!"invert"
			,
				.=,
					a=1
					b=2
					c=3
				.=,
					a=1
					b=2
					c=3
				.=,
					a=1
					b=2
		withTest=*
			!"with"
			,
				object=*
					$
						!"System.Windows.Forms"
						"Form"
					0
				data=,
					Text="hello world"
		restTest=*
			!"rest"
			"hello"
	&=!"test"