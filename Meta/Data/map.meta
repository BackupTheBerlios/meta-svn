integerToString=
	documentation="Converts an integer to a string."
	|
		int=Meta.Integer.[Meta.Map] arg
		this=Meta.StrategyMap.[mscorlib.String] int.ToString *
sort=
	|Meta.Library.Sort arg
pop=
	|Meta.Library.Pop arg
isInteger=
	|
		object=Meta.ObjectMap.[mscorlib.Object] arg
		this=object.IsInteger.get *
isString=
	|
		object=Meta.ObjectMap.[mscorlib.Object] arg
		this=object.IsString.get *
if=
	|
		argument=merge
			=
				else=
					|*
				then=
					|*
			arg
		cases=
			[1]=argument.then
			[*]=argument.else
		this=cases.[argument.condition] *
repeat=
	documentation="Repeats an array a given number of times."
	|
		argument=arg.array
		this=join apply
			array=range arg.times
			function=
				|argument
range=
	documentation="Returns an array containing all numbers from 1 to the argument."
	|
		array=*
		counter=1
		limit=math.add
			arg
			1
		count=
			|
				scope.scope.array.[counter]=counter
				scope.scope.counter=math.add
					counter
					1
		while
			condition=
				|math.smaller
					counter
					limit
			function=
				|
					this=count arg
		this=array
remove=
	documentation="Removes a key from a map."
	|Meta.Library.Remove arg
while=
	documentation="Calls a function as long as the condition function returns 1."
	|
		stop=arg.condition *
		next=
			[*]=
				|*
			=
				|
					arg.function *
					while arg
		next.[stop] arg
while=
	documentation="Calls a function as long as the condition function returns 1."
	|Meta.Library.While arg
foreach=
	|apply
		array=keys arg.map
		function=
			|arg.arg.function
				value=arg.arg.map.[arg]
				key=arg
loop=
	|apply
		array=arg.array
		function=
			|*
apply=
	documentation="Calls a function with every value in an array as an argument and returns the results as an array."
	|applyImplementation
		array=arg.array
		function=arg.function
		index=1
		result=*
	applyImplementation=
		|
			stop=math.greater
				arg.index
				length arg.array
			next=
				=
					|arg.result
				[*]=
					|applyImplementation
						array=arg.array
						function=arg.function
						index=math.add
							arg.index
							1
						object=Meta.ObjectMap.[mscorlib.Object] arg.result
						argument=arg.function arg.array.[arg.index]
						object.Append argument
						result=object.This.get *
			this=next.[stop] arg
join=
	documentation="Concatenates arrays."
	|Meta.Library.Join arg
contains=
	documentation="Determines whether a map contains a key."
	|
		object=Meta.ObjectMap.[mscorlib.Object] arg.map
		this=Meta.StrategyMap.[mscorlib.Boolean] object.ContainsKey arg.key
keys=
	documentation="Returns the keys of a map in an array."
	|
		object=Meta.ObjectMap.[mscorlib.Object] arg
		this=Meta.StrategyMap.[mscorlib.["ICollection`1"].[Meta.Map]] object.Keys.get *
merge=
	documentation="Combines several maps into one."
	|sum
		array=arg
		function=
			|
				overwrite=arg.present
				this=sum
					array=keys arg.present
					function=
						|
							result=arg.previous
							result.[arg.present]=overwrite.[arg.present]
							this=result
					start=arg.previous
		start=*
equal=
	documentation="Determines whether maps are identical."
	|
		result=sum
			array=arg
			function=
				|
					first=Meta.ObjectMap.[mscorlib.Object] arg.previous.previous
					this=
						equality=logic.and
							Meta.StrategyMap.[mscorlib.Boolean] first.Equals arg.present
							arg.previous.equality
						previous=arg.present
			start=
				equality=1
				previous=arg.[1]
		this=result.equality
length=
	documentation="Returns the length of an array."
	|
		map=Meta.ObjectMap.[mscorlib.Object] arg
		this=Meta.StrategyMap.[mscorlib.Int32] map.ArrayCount.get *
sum=
	|sumImplementation
		index=1
		array=arg.array
		function=arg.function
		result=arg.start
	sumImplementation=
		|
			stop=math.greater
				arg.index
				length arg.array
			next=
				=
					|arg.result
				[*]=
					|sumImplementation
						index=math.increment arg.index
						result=arg.function
							previous=arg.result
							present=arg.array.[arg.index]
						array=arg.array
						function=arg.function
			this=next.[stop] arg