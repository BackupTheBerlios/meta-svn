this:map.Merge
	parent
	logic
	map
	math
	System
	System.IO
	System.Windows.Forms
	System.Drawing
choose:
	OpenFile:=
		dialog:OpenFileDialog*
		dialog.ShowDialog*
		this:dialog.FileName.get*
	SaveFile:=
		dialog:SaveFileDialog*
		dialog.ShowDialog*
		this:dialog.FileName.get*
file:
	Write:=
		writer:StreamWriter
			arg.1
		writer.Write
			arg.2
		writer.Close*
	Read:=
		reader:StreamReader
			arg
		result:reader.ReadToEnd*
		reader.Close*
		this:result
fileName:*
SetFileName:=
	parent.parent.fileName:arg
commands:
	SaveAs:=
		saveAs:choose.SaveFile*
		If
			Not Equal
				saveAs
				*
			then:=
				SetFileName saveAs
				file.Write
					fileName
					editBox.RealText.get*
	Save:=If
		Equal
			fileName
			*
		then:=commands.SaveAs*
		else:=file.Write
			fileName
			editBox.RealText.get*
	Open:=
		openFileName:choose.OpenFile*
		If
			Not Equal
				openFileName
				*
			then:=
				SetFileName openFileName
				editBox.RealText.set
					file.Read fileName
	Run:=
		commands.Save*
		Meta.Expression.BreakPoint.set
			Meta.BreakPoint
				fileName
				Meta.Position
					editBox.RealLine.get*
					editBox.Column.get*
		Meta.Interpreter.DebugBreak.add
			=editBox.ShowDebugValue
				Meta.Interpreter.DebugValue
		Meta.Interpreter.StartDebug
			fileName
	Continue:=
		// factor out the setting of the breakpoint
		System.Console.WriteLine
			editBox.RealLine.get*
		System.Console.WriteLine
			editBox.Column.get*
		Meta.Expression.BreakPoint.set
			Meta.BreakPoint
				fileName
				Meta.Position
					editBox.RealLine.get*
					editBox.Column.get*
		Meta.Interpreter.ContinueDebug*
	Reverse:=
		// factor out the setting of the breakpoint
		System.Console.WriteLine
			editBox.RealLine.get*
		System.Console.WriteLine
			editBox.Column.get*
		Meta.Expression.BreakPoint.set
			Meta.BreakPoint
				fileName
				Meta.Position
					editBox.RealLine.get*
					editBox.Column.get*
		Meta.Interpreter.ReverseDebug*
modifierKeys:
	AltControl:bitwise.Or
		Keys.Alt
		Keys.Control
	AltShift:bitwise.Or
		Keys.Alt
		Keys.Shift
	AltControlShift:bitwise.Or
		Keys.Alt
		Keys.Control
		Keys.Shift
	ControlShift:bitwise.Or
		Keys.Control
		Keys.Shift
keyBindings:
	[Keys.Escape]:=editBox.StopInteractiveSearch*
	[Keys.F5]:=commands.Run*
	[Keys.Control]:
		[Keys.S]:=commands.Save*
		[Keys.I]:=editBox.StartInteractiveSearch*
		[Keys.F]:=editBox.FindAndReplace*
	[Keys.Alt]:
		[Keys.K]:=editBox.MoveLineDown*
		[Keys.L]:=editBox.MoveLineUp*
		[Keys.Oemtilde]:=editBox.MoveCharRight*
		[Keys.J]:=editBox.MoveCharLeft*
		[Keys.U]:=editBox.MoveLineStart*
		[Keys.OemSemicolon]:=editBox.MoveLineEnd*
		[Keys.N]:=editBox.DeleteBackward*
		[Keys.M]:=editBox.DeleteForward*
	[modifierKeys.AltControl]:
		[Keys.J]:=editBox.MoveWordLeft*
		[Keys.Oemtilde]:=editBox.MoveWordRight*
		[Keys.OemSemicolon]:=editBox.MoveDocumentEnd*
		[Keys.U]:=editBox.MoveDocumentStart*
	[modifierKeys.AltControlShift]:
		[Keys.J]:=editBox.SelectWordLeft*
		[Keys.Oemtilde]:=editBox.SelectWordRight*
		[Keys.K]:=editBox.SelectLineUp*
		[Keys.J]:=editBox.SelectLineDown*
editBox:
	editBox:ScrollingTextBox*
	editBox.ShowSelectionMargin.set
		"1"
	editBox.AcceptsTab.set
		"1"
	editBox.Dock.set
		DockStyle.Fill
	editBox.ScrollBars.set
		RichTextBoxScrollBars.ForcedBoth
	editBox.KeyDown.add
		:
			#
				System.Console.WriteLine
					"key was pressed"
				e:arg.2
				// maybe this should be the other way round ???
				modifiers:e.Modifiers.get*
				keyCode:e.KeyCode.get*
				If
					ContainsKey
						keyBindings
						modifiers
					then:=
						subKey:keyBindings.[modifiers]
						If
							ContainsKey
								subKey
								keyCode
							then:=
								subKey.[keyCode]*
					else:=
						If
							ContainsKey
								keyBindings
								keyCode
							then:=keyBindings.[keyCode]*
	this:editBox
form:
	form:Form*
	form.Text.set
		"Meta Edit"
	form.Font.set
		Font
			"Courier New"
			"10"
	controls:form.Controls.get*
	controls.AddRange
		:
			editBox
	form.WindowState.set
		FormWindowState.Maximized
	menu:
		File:
			["Open..."]:commands.Open
			["Save"]:commands.Save
			["Save as..."]:commands.SaveAs
			["Close"]:=form.Close*
		Program:
			Start:commands.Run
			Continue:commands.Continue
			Reverse:commands.Reverse
	form.Menu.set
		MainMenu 
			Apply
				=MenuItem
					arg.key
					Apply
						=MenuItem
							arg.key
							arg.value
						arg.value
				menu
	this:form
form.ShowDialog*