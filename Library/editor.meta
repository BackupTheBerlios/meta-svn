this:map.Merge
	parent
	logic
	map
	math
	System
	System.IO
	System.Windows.Forms
	System.Drawing
choose:
	OpenFile:=
		dialog:OpenFileDialog*
		dialog.ShowDialog*
		this:dialog.FileName.get*
	SaveFile:=
		dialog:SaveFileDialog*
		dialog.ShowDialog*
		this:dialog.FileName.get*
file:
	Write:=
		writer:StreamWriter
			arg.1
		writer.Write
			arg.2
		writer.Close*
	Read:=
		reader:StreamReader
			arg
		result:reader.ReadToEnd*
		reader.Close*
		this:result
fileName:*
SetFileName:=
	parent.parent.fileName:arg
	form.Text.set
		Append
			"Meta Edit - "
			fileName
SetBreakPoint:=Meta.Expression.BreakPoint.set
	Meta.BreakPoint
		fileName
		Meta.Position
			editBox.RealLine.get*
			editBox.Column.get*
commands:
	SaveAs:=
		saveAs:choose.SaveFile*
		If
			Not Equal
				saveAs
				*
			then:=
				SetFileName saveAs
				file.Write
					fileName
					editBox.RealText.get*
	Save:=If
		Equal
			fileName
			*
		then:=commands.SaveAs*
		else:=file.Write
			fileName
			editBox.RealText.get*
	Open:=
		openFileName:choose.OpenFile*
		If
			Not Equal
				openFileName
				*
			then:=
				SetFileName openFileName
				editBox.RealText.set
					file.Read fileName
	Run:=
		commands.Save*
		SetBreakPoint*
		Meta.Interpreter.DebugBreak.add
			=editBox.ShowDebugValue
				Meta.Interpreter.DebugValue.get*
		Meta.Interpreter.StartDebug
			fileName
	Continue:=
		SetBreakPoint*
		Meta.Interpreter.ContinueDebug*
	Reverse:=
		SetBreakPoint*
		Meta.Interpreter.ReverseDebug*
// TODO: maybe nest the modifiers inside each other?
Modifiers:
	ControlAlt:bitwise.Or
		Keys.Alt
		Keys.Control
	ShiftAlt:bitwise.Or
		Keys.Alt
		Keys.Shift
	// TODO: rename to a more logical order
	ControlShiftAlt:bitwise.Or
		Keys.Alt
		Keys.Control
		Keys.Shift
	ControlShift:bitwise.Or
		Keys.Control
		Keys.Shift
shortcuts:
	[Keys.Escape]:=editBox.StopInteractiveSearch*
	[Keys.F5]:=commands.Run*
	[Keys.Up]:=editBox.MoveLineUp*
	[Keys.Down]:=editBox.MoveLineDown*
	[Keys.Left]:=editBox.MoveCharLeft*
	[Keys.Right]:=editBox.MoveCharRight*
	[Keys.Home]:=editBox.MoveLineStart*
	[Keys.End]:=editBox.MoveLineEnd*
	[Keys.Control]:
		[Keys.S]:=commands.Save*
		[Keys.I]:=editBox.StartInteractiveSearch*
		[Keys.H]:=editBox.FindAndReplace*
		[Keys.Left]:=editBox.MoveWordLeft*
		[Keys.Right]:=editBox.MoveWordRight*
		[Keys.Back]:=editBox.DeleteWordLeft*
		[Keys.Delete]:=editBox.DeleteWordRight*
		[Keys.Home]:=editBox.MoveDocumentStart*
		[Keys.End]:=editBox.MoveDocumentEnd*
	[Keys.Alt]:
		[Keys.K]:=editBox.MoveLineDown*
		[Keys.L]:=editBox.MoveLineUp*
		[Keys.Oemtilde]:=editBox.MoveCharRight*
		[Keys.J]:=editBox.MoveCharLeft*
		[Keys.U]:=editBox.MoveLineStart*
		[Keys.OemSemicolon]:=editBox.MoveLineEnd*
		[Keys.N]:=editBox.DeleteBackward*
		[Keys.M]:=editBox.DeleteForward*
	[Modifiers.ShiftAlt]:
		[Keys.J]:=editBox.SelectCharLeft*
		[Keys.Oemtilde]:=editBox.SelectCharRight*
		[Keys.L]:=editBox.SelectLineUp*
		[Keys.K]:=editBox.SelectLineDown*
	[Modifiers.ControlAlt]:
		[Keys.J]:=editBox.MoveWordLeft*
		[Keys.Oemtilde]:=editBox.MoveWordRight*
		[Keys.OemSemicolon]:=editBox.MoveDocumentEnd*
		[Keys.U]:=editBox.MoveDocumentStart*
		[Keys.N]:=editBox.DeleteWordLeft*
		[Keys.M]:=editBox.DeleteWordRight*
	[Modifiers.ControlShiftAlt]:
		[Keys.J]:=editBox.SelectWordLeft*
		[Keys.Oemtilde]:=editBox.SelectWordRight*
editBox:
	editBox:ScrollingTextBox*
	editBox.ShowSelectionMargin.set
		"1"
	editBox.AcceptsTab.set
		"1"
	editBox.Dock.set
		DockStyle.Fill
	editBox.ScrollBars.set
		RichTextBoxScrollBars.ForcedBoth
	editBox.KeyDown.add
		:
			#
				e:arg.2
				// maybe this should be the other way round ???
				modifiers:e.Modifiers.get*
				keyCode:e.KeyCode.get*
				If
					ContainsKey
						shortcuts
						modifiers
					then:=
						subKey:shortcuts.[modifiers]
						If
							ContainsKey
								subKey
								keyCode
							then:=
								subKey.[keyCode]*
								e.Handled.set
									"1"
					else:=
						If
							ContainsKey
								shortcuts
								keyCode
							then:=
								shortcuts.[keyCode]*
								e.Handled.set
									"1"
	this:editBox
form:
	form:Form*
	form.Text.set
		"Meta Edit"
	form.Font.set
		Font
			"Courier New"
			"10"
	controls:form.Controls.get*
	controls.AddRange
		:
			editBox
	form.WindowState.set
		FormWindowState.Maximized
	menu:
		File:
			["Open..."]:commands.Open
			["Save"]:commands.Save
			["Save as..."]:commands.SaveAs
			["Close"]:=form.Close*
		Program:
			Start:commands.Run
			Continue:commands.Continue
			Reverse:commands.Reverse
	form.Menu.set
		MainMenu
			Apply
				=MenuItem
					arg.key
					Apply
						=MenuItem
							arg.key
							arg.value
						arg.value
				menu
	this:form
form.ShowDialog*